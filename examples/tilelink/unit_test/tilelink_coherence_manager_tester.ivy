#lang ivy1.3

################################################################################
# 
# Test generator for a TileLink coherence manager. The coherence manager
# has a coherent TileLink interface on inside and non-coherent main memory
# interface on outside.
#
################################################################################

include tilelink_concrete_spec

################################################################################
#
# Generic model of a client. This performs arbitrary client actions, except
# the it guarantees to use only ID's from the "client" side of the interface
# as defined by its parameter "side".
#
# TODO: should be able to generate this
#
################################################################################

module generic_client_intf(mngr) = {

   action tl_Grant(msg:gnt_id) = {}

   action tl_Probe(msg:prb_id) = {}

   action acquire(
           cid : client_id,        # client txid
           id_ : id,               # id of requester
	   addr_hi : tl_addrhi,    # hi order bits of address
           word : tl_addrlo,       # requested word if uncached
           own:ownership,          # what ownership type? (none=uncached)
           op : otype,             # what operation?
           data_ : data,           # what data, if any?
	   block : bool,           # is this a full block request?

           ltime_ : ltime         # (aux) logical time
   ) = {
       local a:acq_id 
       {
           acqs(a).cid := cid;
           acqs(a).id_ := id_;
	   acqs(a).addr_hi := addr_hi;
           acqs(a).word := word;
           acqs(a).own := own;
           acqs(a).op := op;
           acqs(a).data_ := data_;
	   acqs(a).block := block;
           acqs(a).ltime_ := ltime_;
	   call mngr.tl_Acquire(a)
       }
   }

   action finish(
           cid : client_id,        # client txid (aux?)
           id_ : id,               # transaction id
           addr_hi : tl_addrhi,    # (aux) hi order bits of acquire
           word : tl_addrlo,       # (aux) low-order bits of acquire
           own:ownership           # (aux) ownership or acquire
   ) = {
       local a:fns_id
       {
           fnss(a).cid := cid;
           fnss(a).id_ := id_;
           fnss(a).addr_hi := addr_hi;
           fnss(a).word := word;
           fnss(a).own := own;
	   call mngr.tl_Finish(a)
       }
   }

   action release(
           cid : client_id,        # client txid
           id_ : id,             # id of requester
           voluntary : bool,     # is this a voluntary release?
           addr_hi : tl_addrhi,  # hi order bits of address
           word :tl_addrlo,      # word being released
           dirty : bool,         # is data modified?
           data_ : data         # data, if dirty
   ) = {
        local a:rls_id
        {
           rlss(a).cid := cid;
           rlss(a).id_ := id_;
           rlss(a).voluntary := voluntary;
           rlss(a).addr_hi := addr_hi;
           rlss(a).word := word;
           rlss(a).dirty := dirty;
           rlss(a).data_ := data_;
	   call mngr.tl_Release(a)
        }
   }
}

module tl_generic_client_gen(mngr,ref,side) = {

   instantiate generic_client_intf(mngr)

   action perform(lt:ltime, sid:id) = {
       assume side(sid) = client;
       call ref.perform(lt,sid)
   }

   action fuse(lt:ltime, lt1:ltime, sid:id) = {
       assume side(sid) = client;
       call ref.fuse(lt,lt1,sid)
   }
}

################################################################################
#
# Generic model of a manager. This performs arbitrary manager actions, except
# the it guarantees to use only ID's from the "manager" side of the interface
# as defined by its parameter "side".
#
# TODO: should be able to generate this
#
################################################################################

module generic_manager_intf(clnt) = {

   action tl_Acquire(msg:acq_id) = {}

   action tl_Finish(msg:fns_id) = {}

   action tl_Release(msg:rls_id) = {}

   action grant(
           cid : client_id,        # client txid
           clnt_txid : id,               # client transaction id
           mngr_txid : id,               # manager transaction id
           word : tl_addrlo,       # low-order bits of this response
           own : ownership,          # what ownership type? (none = uncached)
           relack : bool,          # is this an ack of a Release?
           data_ : data,           # return value, if any

           addr_hi : tl_addrhi,    # (aux) hi order bits of address
           ltime_ : ltime         # (aux) logical time
   ) = {
       local a:gnt_id
       {
           gnts(a).cid := cid;
           gnts(a).clnt_txid := clnt_txid;
           gnts(a).mngr_txid := mngr_txid;
           gnts(a).word := word;
           gnts(a).own := own;
           gnts(a).relack := relack;
           gnts(a).data_ := data_;

           gnts(a).addr_hi := addr_hi;
           gnts(a).ltime_ := ltime_;
	   call clnt.tl_Grant(a)
       }
   }

   action probe(
           cid : client_id,        # client txid
           id_ : id,             # id of receiver
           addr_hi : tl_addrhi  # hi order bits of address
   ) = {
       local a:prb_id
       {
           prbs(a).cid := cid;
           prbs(a).id_ := id_;
           prbs(a).addr_hi := addr_hi;
           call clnt.tl_Probe(a)
       }
   }
}

module tl_generic_manager_gen(clnt,ref,side) = {
 
   instantiate generic_manager_intf(clnt)

   action perform(lt:ltime, sid:id) = {
       assume side(sid) = manager;
       call ref.perform(lt,sid)
   }

   action fuse(lt:ltime, lt1:ltime, sid:id) = {
       assume side(sid) = manager;
       call ref.fuse(lt,lt1,sid)
   }

}

################################################################################
# 
# The system we want to verify. This is a "block of wood", since we are just
# generating inputs, not verifying anything. The block of wood is implemented
# by combining a generic client and a generic manager.
#
################################################################################


module wood_block(clnt,mngr) = {
   instantiate generic_client_intf(mngr)
   instantiate generic_manager_intf(clnt)
}

individual buf_id : id   # arbitrary process id for the dut

instantiate c : tl_generic_client_gen(b,ref,fside)
instantiate b : wood_block(c,m)
instantiate m : tl_generic_manager_gen(b,ref,bside)

export c.acquire
export c.finish
export c.release
export c.perform
export m.grant
export m.probe
export b.acquire
export b.finish
export b.release
export b.grant
export b.probe

################################################################################
# 
# Instantiate the reference specification
#
################################################################################

instantiate ref : reference

################################################################################
# 
# Specify the two interfaces of the buffer
#
# To do this, we arbitrarily distribute the process id's on the client and
# manager side. The axioms guarantee the side assignments for the front
# and back interfaces of the buffer are consistent.
#
################################################################################

individual fside(I:id) : side
individual bside(I:id) : side
individual cmap(I:id) : client_id
axiom fside(buf_id) = manager
axiom bside(buf_id) = client
axiom I ~= buf_id -> fside(I) = bside(I)
individual c_id : id
axiom fside(c_id) = client

# All memory events are on the client side of the coherence manager

axiom fside(ref.evs(X).id_) = client

instantiate front : tl_interface(ref,c,b,fside,cmap)
instantiate back : tl_mm_interface(ref,b,m,bside)

################################################################################
#
# Localize the proof
#
################################################################################

isolate iso_b = b with ref,front,back,c,m

################################################################################
#
# Liveness checks
#
################################################################################

# BOGUS: these are set directly by the test environment

relation front_acq_rdy, front_gnt_rdy, front_fns_rdy, front_rls_rdy, front_prb_rdy
relation back_acq_rdy, back_gnt_rdy, back_fns_rdy, back_rls_rdy, back_prb_rdy

progress front_acq_excl(C,A) = ~front.excl_r(C,A)
#progress front_acq_shrd(C,A) = ~front.shrd_r(C,A)
#progress front_acq_unc(C,A,W) = ~front.unc_r(C,A,W)
progress back_acq_rdy_io = back_acq_rdy

progress back_acq_unc_r(A) = forall W:tl_addrlo. ~back.unc_r(A,W:tl_addrlo)
progress back_acq_unc_w(A,W) = back.unc_wb(A) | ~back.unc_w(A,W)
progress back_acq_unc_wb(A) = ~back.unc_wb(A)

progress front_prb_rls(C,A) = forall W:tl_addrlo. ~front.prb_addr(C,A,W:tl_addrlo)
progress front_prb_rdy_io = front_prb_rdy

# front grant liveness

progress front_gnt_rdy_io = front_gnt_rdy
progress front_gnt_fns_excl(C,A) = ~front.excl_f(C,A)
progress front_gnt_fns_shrd(C,A) = ~front.shrd_f(C,A)
progress front_gnt_fns_unc(C,A,W) = ~front.unc_f(C,A,W)


# rely/guarantee dependencies

rely front_acq_excl(C,A) -> back_acq_rdy_io
rely front_acq_excl(C,A) -> front_prb_rls(C1,A1)
rely front_acq_excl(C,A) -> front_prb_rdy_io
rely front_acq_excl(C,A) -> back_acq_unc_r(A1)
rely front_acq_excl(C,A) -> back_acq_unc_w(A1,W)
rely front_acq_excl(C,A) -> back_acq_unc_wb(A1)
rely front_acq_excl(C,A) -> front_gnt_rdy_io
rely front_acq_excl(C,A) -> front_gnt_fns_excl(C1,A1)
rely front_acq_excl(C,A) -> front_gnt_fns_shrd(C1,A1)
rely front_acq_excl(C,A) -> front_gnt_fns_unc(C1,A1,W)

# rely front_acq_shrd(C,A) -> back_acq_rdy_io
# rely front_acq_shrd(C,A) -> front_prb_rls(C,A)
# rely front_acq_shrd(C,A) -> front_prb_rdy_io
# rely front_acq_shrd(C,A) -> back_acq_unc_r(A)
# rely front_acq_shrd(C,A) -> back_acq_unc_w(A,W)
# rely front_acq_shrd(C,A) -> back_acq_unc_wb(A)
# rely front_acq_shrd(C,A) -> front_gnt_rdy_io
# rely front_acq_shrd(C,A) -> front_gnt_fns_excl(C,A)
# rely front_acq_shrd(C,A) -> front_gnt_fns_shrd(C,A)
# rely front_acq_shrd(C,A) -> front_gnt_fns_unc(C,A,W)


# these are guaranteed by the environment

rely back_acq_rdy_io
rely front_prb_rls(C,A)
rely back_acq_unc_r(A)
rely back_acq_unc_w(A,W)
rely back_acq_unc_wb(A)
rely front_gnt_rdy_io
rely front_gnt_fns_excl(C,A)
rely front_gnt_fns_shrd(C,A)
rely front_gnt_fns_unc(C,A,W)
rely front_prb_rdy_io

################################################################################
#
# Finitely instantiate the types for testing purposes
#
################################################################################

interpret time -> bv[2]
interpret ltime -> bv[2]
interpret data -> {0..1}
interpret addr -> {0..3}
interpret id -> {0..2}
interpret tl_addrhi -> {0..1}
interpret tl_addrlo -> bv[1]
interpret acq_id -> {0..0}
interpret gnt_id -> {0..0}
interpret fns_id -> {0..0}
interpret rls_id -> {0..0}
interpret prb_id -> {0..0}
interpret client_id -> {0..1}

