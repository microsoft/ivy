#lang ivy1.6

module udp_wrapper(addr,pkt,me,port_base) = {

    object rdr = {}

    <<< header
	class udp_reader;
    >>>
    <<< impl
	class udp_reader : public reader {
	    int sock;
	    int my_id;
	    %`handle_recv` rcb;
	  public:
	    udp_reader(int _my_id, %`handle_recv` rcb)
	        : my_id(_my_id), rcb(rcb) {
		sock = socket(AF_INET, SOCK_DGRAM, 0);
		if (sock < 0)
		    { std::cerr << "cannot create socket\n"; exit(1); }

		struct sockaddr_in myaddr;
		memset((char *)&myaddr, 0, sizeof(myaddr));
		myaddr.sin_family = AF_INET;
		myaddr.sin_addr.s_addr = htonl(INADDR_ANY);
		myaddr.sin_port = htons(`port_base`+my_id);
		if (bind(sock, (struct sockaddr *)&myaddr, sizeof(myaddr)) < 0)
		    { std::cerr << "bind failed\n"; exit(1); }

	    }

	    virtual int fdes() {
		return sock;
	    }
	    virtual void read() {
		//std::cout << "RECEIVING\n";
	        int len=0;
                socklen_t lenlen=4;
	        if (getsockopt(sock,SOL_SOCKET,SO_RCVBUF,&len,&lenlen))
	            { perror("getsockopt failed"); exit(1); }
	        std::vector<char> buf(len);
	        int bytes;
		if ((bytes = recvfrom(sock,&buf[0],sizeof(`pkt`),0,0,0)) < 0)
		    { std::cerr << "recvfrom failed\n"; exit(1); }
	        buf.resize(bytes);
	        `pkt` pkt;
	        try {
	            unsigned pos = 0;
		    __deser(buf,pos,pkt);
	            if (pos < buf.size())
	                throw deser_err();
                } catch (deser_err &){
		    std::cout << "BAD PACKET RECEIVED\n";
		    return;
		}
		rcb(pkt);
	    }
	    virtual void write(int dst, `pkt` pkt) {
		struct sockaddr_in dstaddr;
		memset((char *)&dstaddr, 0, sizeof(dstaddr));
		dstaddr.sin_family = AF_INET;
		dstaddr.sin_addr.s_addr = htonl(INADDR_ANY);
		dstaddr.sin_port = htons(`port_base`+dst);

	        std::vector<char> buf;
	        __ser(buf,pkt);
		//std::cout << "SENDING\n";
		if (sendto(sock,&buf[0],buf.size(),0,(sockaddr *)&dstaddr,sizeof(sockaddr_in)) < 0) 
		     { std::cerr << "sendto failed\n"; exit(1); }
	    }
	};
    >>>
    <<< member
	udp_reader *`rdr`;
    >>>
    <<< init
	install_reader(`rdr` = new udp_reader(`me`,`handle_recv`));
    >>>

    action handle_recv(x:pkt) = {
	call recv(me,x)
    }

    implement send(dst:addr,x:pkt) {
	<<< impure
	    `rdr`->write(`dst`,`x`);
	>>>
    }
}

module udp_simple(addr,pkt) = {

    action recv(dst:addr,v:pkt)
    action send(src:addr,dst:addr,v:pkt)

    object spec = {
        relation sent(V:pkt, N:addr)

        after init {
	    sent(V, N) := false
	}
    
	before send {
	    sent(v,dst) := true
	}
	before recv {
	    assert sent(v,dst)
	}
    }

    instance impl(X:addr) : udp_wrapper(addr,pkt,X,4990)
}


module nondup_endpoint(port,pkt) = {

    action recv(v:pkt)
    action send(v:pkt)

    object spec = {
        relation sent(V:pkt)

        after init {
	    sent(V) := false
	}
    
	before send {
	    assert ~sent(v);
	    sent(v) := true
	}
	before recv {
	    assert sent(v);
	    sent(v) := false
	}
    }

    object impl = {
	action internal(p:pkt) = {
	    call recv(p);
	}
    }

    trusted isolate iso = impl with spec

}


module nondup_endpoint_set(addr,port_base,pkt) = {
    
    action recv(dst:addr,v:pkt)
    action send(dst:addr,v:pkt)

    object spec = {
        relation sent(V:pkt, N:addr)

        after init {
	    sent(V, N) := false
	}
    
	before send {
	    assert ~sent(v,dst);
	    sent(v,dst) := true
	}
	before recv {
	    assert sent(v,dst);
	    sent(v,dst) := false
	}
    }

    object impl = {
	action internal(dst:addr,p:pkt) = {
	    call recv(dst,p);
	}
    }

    trusted isolate iso = impl with spec

}


# type a
# type t

# instance foo : udp_simple(a,t)

# interpret a -> bv[1]

# import foo.recv
# export foo.send

# extract iso_foo(me) = foo(me)
