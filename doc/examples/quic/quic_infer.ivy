#lang ivy1.7

#
# Ghost event inference
# ---------------------
#
# As an aid in monitoring QUIC packet streams, this section provides
# actions for inferring unobserved ghost events from QUIC packet
# events.

# From a QUIC packet event, infer unobserved TLS events

#
# TODO: as of version 11, each role has its own cid, so connections
# are not renamed at the clinet side on a server hello. This code
# is commented and should be removed [1].
#

action infer_tls_events(src:ip.endpoint,dst:ip.endpoint,pkt:quic_packet) = {

    # Stream frames on id 0 indicate that a `tls_send_event` has occurred.
    # TODO: ignore duplicate frames.

    var scid := pkt.src_cid;
    var dcid := pkt.dst_cid;

    # On short headers, the scid is not given, so we use the recorded value

    if ~pkt.long {
        scid := connected_to(dcid)
    };

    # If this is an initial client request, we infer the nonce choice

    if pkt.ptype = quic_packet_type.initial & ~conn_seen(dcid) {
        nonce_cid(scid) := dcid;
    };

    var e := pkt.ptype;
    
    call set_encryption_level(src,scid,e);

    # infer sender of packet has established keys for the packet

    call tls_keys_established_event(scid,e);

    # Infer an app_server_open_event.
    if conn_requested(dst,src,dcid) & ~connected(dcid) & ~connected(scid) {
        call app_server_open_event(src,dst,scid,dcid);
    };

    var idx := pkt.payload.begin;
    while idx < pkt.payload.end {
	var f := pkt.payload.value(idx);
	if some(cf:frame.crypto) f *> cf {
	    call tls_send_event(src,dst,scid,dcid,cf.data,cf.offset,e)
	};
	if some(sf:frame.stream) f *> sf {
            # require sf.offset <= stream_app_data_end(dcid,sf.id);  # TODO: deal with gaps in stream
            # var data := sf.data.segment(stream_app_data_end(dcid,sf.id) - sf.offset,sf.data.end);
	    call app_send_event(src,dst,dcid,sf.id,sf.data,sf.offset,sf.fin)
	};
	call show_frame(src,dst,scid,dcid,f);
	call f.handle(src,dst,scid,dcid,e);
        idx := idx.next
    }
}

action show_frame(src:ip.endpoint,dst:ip.endpoint,scid:cid,dcid:cid,f:frame)

import show_frame
