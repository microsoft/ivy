#lang ivy1.6

include tcp

type data

module replica = {
    individual value : data
    after init {
	value := 0;
    }
    action upd(inp : data) returns (out : data) = {
	out := value;
	value := value + inp;
    }
}

module reference = {
    ghost type txid
    instance rep : replica

    action create(inp : data) returns (tx : txid)
    action serialize(tx : txid)
    action commit(tx : txid)
    action eval(tx : txid) returns (res : data)

    individual next : txid
    function txs(X:txid) : data
    function txres(X:txid) : data
    relation serialized(X:txid)
    relation committed(X:txid)

    after init {
	next := 0;
	committed(X) := false;
	serialized(X) := false;
    }

    implement create {
	tx := next;
	txs(tx) := inp;
	next := next + 1;
    }

    implement serialize {
	assert 0 <= tx & tx < next;
	assert ~serialized(tx);
	serialized(tx) := true;
    }
    delegate serialize

    implement commit {
	assert 0 <= tx & tx < next;
	assert serialized(tx) & ~committed(tx);
	txres(tx) := rep.upd(txs(tx));
	committed(tx) := true;
    }
    delegate commit

    implement eval {
	assert committed(tx);
	res := txres(tx);
    }
    delegate eval

    interpret txid -> int
}

instance ref : reference

type msg = struct {
    val : data,
    tx : ref.txid
}    


# module client(req_chan,rsp_chan) = {
    
#     action request_upd(val : data) = {
# 	call req_chan.sender.send(val);
#     }

#     action response_upd(val : data)

#     implement rsp_chan.receiver.recv(val : data) {
# 	call response_upd(val : data)
#     }
# }

module client(out_chan) = {
    
    action request_upd(val : data) returns (tx : ref.txid)

    implement request_upd {
	local m : msg {
	    m.val := val;
	    m.tx := tx;
	    call out_chan.send(m);
	}
    }
}

module head_node(req_chan1, req_chan2, out_chan, sysout_chan) = {
    instance rep : replica

    action upd(inp : msg) = {
	call ref.serialize(inp.tx);           # this is ghost!
	var res := rep.upd(inp.val);  # ignore the result
	if ~master.tail_failed {
	    call out_chan.send(inp);
	}
	else {
	    inp.val := res;
	    call sysout_chan.send(inp);
	}	    
    }
	
    implement req_chan1.recv(inp : msg) {
	call upd(inp);
    }
    implement req_chan2.recv(inp : msg) {
	call upd(inp);
    }
}

module tail_node(inp_chan, out_chan) = {
    instance rep : replica

    action upd(inp : msg) = {
	if ~master.tail_failed {
	    local out : msg {
		out.val := rep.upd(inp.val);
		out.tx := inp.tx;
		call out_chan.send(out);
	    }
	}
    }
	
    implement inp_chan.recv(inp : msg) {
	call upd(inp);
    }
}

object master = {
    individual tail_failed : bool

    after init {
	tail_failed := false;
    }

    action tail_fail = {
	tail_failed := true;
    }

    object spec = {
	before tail_fail {
	    assert ~tail_failed;
	}
    }
}

instance clrq1 : tcp_channel("localhost:44090",msg)
instance clrq2 : tcp_channel("localhost:44091",msg)
instance headtail : tcp_channel("localhost:44092",msg)
instance sysout : tcp_channel("localhost:44093",msg)

instance cl1 : client(clrq1.sndr)
instance cl2 : client(clrq2.sndr)
instance head : head_node(clrq1.rcvr,clrq2.rcvr,headtail.sndr,sysout.sndr)
instance tail : tail_node(headtail.rcvr,sysout.sndr)


object inspec = {
    before cl1.request_upd(inp : data) returns (tx:ref.txid) {
	tx := ref.create(inp);
    }
    before cl2.request_upd(inp : data) returns (tx:ref.txid) {
	tx := ref.create(inp);
    }
}

object outspec = {
    before sysout.sndr.send(m : msg) {
	assert m.val = ref.eval(m.tx);
    }
}

object htspec = {
    instance queue : unbounded_queue(ref.txid)

    after ref.serialize(tx:ref.txid) {
	call queue.push(tx);
	if master.tail_failed {
	    call ref.commit(tx);
	}
    }

    before headtail.rcvr.recv(m : msg) {
	assert m.val = ref.txs(m.tx);
	assert m.tx = queue.pop;
	call ref.commit(m.tx);
    }
    delegate headtail_rcvr_recv[before] -> head

    after master.tail_fail {
        while ~queue.empty {
	    var tx := queue.pop;
	    call ref.commit(tx);
	}
    }

}

export cl1.request_upd
export cl2.request_upd
import sysout.rcvr.recv
export master.tail_fail

trusted isolate iso_head = head with cl1,cl2,clrq1,clrq2,ref,inspec,htspec,outspec,master,headtail
trusted isolate iso_tail = tail with ref,htspec,outspec,master

interpret data -> bv[16]

attribute master.tail_fail.weight = "0.05"
